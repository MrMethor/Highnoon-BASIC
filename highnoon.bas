100 REM DESIGNED AND PROGRAMMED BY CHRIS GAYLO, SYOSSET H.S.**9/12/70
110 PRINT "                     H I G H  N O O N"
120 PRINT "                     ----------------"
130 PRINT
135 PRINT
140 PRINT "(ONLY CAPITAL LATTERS ARE ACCEPTED)"
145 PRINT
150 PRINT "DO YOU WANT INSTRUCTIONS";
160 INPUT D$
170 IF D$ = "NO" THEN 460
180 PRINT
190 PRINT
200 PRINT "YOU HAVE BEEN CHALLENGED TO A SHOWDOWN BY BLACK BART, ONE OF"
210 PRINT "THE MEANEST DESPERADOES WEST OF THE ALLEGHENY MOUNTAINS."
220 PRINT "WHILE YOU ARE WALKING DOWN A DUSTY, DESERTED SIDE STREET,"
230 PRINT "BLACK BART EMERGES FROM A SALOON ONE HUNDRED PACES AWAY. BY"
240 PRINT "AGREEMENT, YOU EACH HAVE FOUR CARTRIDGES IN YOUR SIX-GUNS."
250 PRINT "YOUR MARKSMANSHIP EQUALS HIS. AT THE START OF THE WALK, NEI-"
260 PRINT "THER OF YOU CAN POSSIBLY HIT THE OTHER, AND AT THE END OF"
270 PRINT "THE WALK, NEITHER CAN MISS. THE CLOSER YOU GET, THE BETTER"
280 PRINT "YOUR CHANCES OF HITTING BART, BUT HE ALSO HAS BETTER CHANCES"
290 PRINT "OF HITTING YOU."
300 PRINT "DO YOU STILL WANT TO CONTINUE";
310 INPUT A$
320 IF A$ = "NO" THEN 1370
330 PRINT "THE MOVES ARE AS FOLLOWS:"
340 PRINT
350 PRINT
360 PRINT "                     *M O V E S*"
370 PRINT "                     ==========="
380 PRINT
390 PRINT "                     1. ADVANCE"
400 PRINT
410 PRINT "2. STAND STILL                             3. FIRE"
420 PRINT
430 PRINT "4. JUMP BEHIND THE WATERING TROUGH         5. GIVE UP"
440 PRINT
450 PRINT "                     6. TURN TAIL AND RUN"
455 PRINT
456 PRINT
460 PRINT "WHAT IS YOUR LUCKY NUMBER FOR TODAY";
470 INPUT A
480 A = 1E4 * ABS(SIN(A))
490 A = (A - INT(A)) * 1000
500 FOR I = 1 TO A
510 B = RND(0)
520 NEXT I
530 PRINT
540 LET X = 100
550 PRINT
560 PRINT "WHAT IS YOUR STRATEGY";
570 INPUT B
580 B = INT(B)
590 IF B > 6 THEN 1340
600 IF B < 1 THEN 1340
610 IF B = 1 THEN 620
612 IF B = 2 THEN 1390
614 IF B = 3 THEN 690
616 IF B = 4 THEN 1410
618 IF B = 5 THEN 1510
619 IF B = 6 THEN 1610
620 PRINT "HOW MANY PACES DO YOU ADVANCE";
630 INPUT S
640 IF S < 0 THEN 2000
650 IF S > 10 THEN 1980
660 X = X - S
670 PRINT "YOU ARE NOW";X;"PACES APART."
680 GOTO 1040
690 C = C + 1
700 IF C <= 4 THEN 750
710 PRINT "NICE GOING, ACE, YOU'VE RUN OUT OF SHELLS."
720 PRINT "NOW BART WON'T SHOOT UNTIL YOU TOUCH NOSES."
730 PRINT "YOU BETTER THINK OF SOMETHING FAST. (LIKE RUN)"
740 GOTO 1040
750 W = RND(-1) * 10
760 IF W > X / 10 THEN 820
770 IF C = 2 THEN 1820
780 IF C = 3 THEN 1840
790 PRINT "WHAT A LOUSY SHOT."
800 IF C = 4 THEN 710
810 GOTO 1460
820 PRINT "WHAT A SHOT, YOU GOT BLACK BART RIGHT BETWEEN THE EYES."
830 PRINT
840 PRINT
850 PRINT "AS MAYOR OF DODGE CITY, AND ON BEHALF OF ITS CITIZENS,"
860 PRINT "I EXTEND TO YOU OUR THANKS, AND PRESENT YOU WITH THIS"
870 PRINT "REWARD, A CHECK FOR $20,000, FOR KILLING BLACK BART."
880 PRINT
890 PRINT "***************************************************************"
900 PRINT
910 PRINT "CHECK NO." 1000 * RND(-1) "         AUG." INT(10 + 10 * RND(0)) "TH. 1889"
920 PRINT "         CASHIER'S RECEIT---BANK OF DODGE CITY"
930 PRINT
940 PRINT "           PAY TO THE BEARER ON DEMAND"
950 PRINT
960 PRINT "                   THE SUM OF"
970 PRINT
980 PRINT "         TWENTY THOUSAND DOLLARS------$20,000"
990 PRINT
1000 PRINT "*************************************************************"
1010 PRINT
1020 PRINT "DON'T SPEND IT ALL IN ONE PLACE."
1030 GOTO 2080
1040 IF X < 10 THEN 1120
1050 Q = RND(-1) * 10
1060 IF Q > 5 THEN 1120
1070 Z = INT(2 + RND(-1) * 9)
1080 PRINT "BLACK BART MOVES" Z "PACES"
1090 X = X - Z
1100 PRINT "YOU ARE NOW" X "PACES APART"
1110 GOTO 550
1120 P = P + 1
1130 IF C < 4 THEN 1070
1140 IF P <= 4 THEN 1200
1150 IF P = 5 THEN 1180
1160 IF P > 6 THEN 2020
1170 GOTO 1070
1180 PRINT "NOW IS YOUR CHANCE, BART IS OUT OF SHELLS"
1190 GOTO 1070
1200 R = RND(-I) * 10
1210 PRINT "BART FIRES . . . . . ."
1220 IF R > X / 10 THEN 1300
1230 PRINT "A MISS . . . ."
1240 IF P = 2 THEN 1890
1250 IF P = 3 THEN 1940
1260 IF P > 4 THEN 1070
1270 IF P = 4 THEN 1960
1280 PRINT "WHEW, WERE YOU LUCKY. THAT BULLET JUST MISSED YOUR HEAD."
1290 GOTO 1490
1300 IF B = 4 THEN 1910
1310 PRINT "BART SHOT YOU RIGHT THROUGH THE HEART THAT TIME."
1320 PRINT "YOU WENT KICKIN' WITH YOUR BOOTS ON."
1330 GOTO 2080
1340 PRINT "YOU SURE AREN'T GOING TO LIVE VERY LONGIF YOU CAN'T EVEN"
1350 PRINT "FOLLOW DIRECTIONS"
1360 GOTO 550
1370 PRINT "GREENHORN."
1380 GOTO 2080
1390 PRINT "THAT MOVE MADE YOU A PERFECT STATIONARY TARGET"
1400 GOTO 1040
1410 T = T + 1
1420 IF T > 3 THEN 2060
1430 IF T > 3 THEN 2060
1440 PRINT "NOT A BAD MANEUVER. YOU THREW BART'S STRATEGY OFF"
1450 GOTO 1040
1460 IF P > 4 THEN 1050
1470 PRINT "YOU NOW HAVE";4-C;"SHELLS TO BART'S";4-P;"SHELLS."
1480 GOTO 1040
1490 PRINT "YOU NOW HAVE"4-C"SHELLS TO BART'S"4-P"SHELLS."
1500 GOTO 550
1510 PRINT "BLACK BART ACCEPTS THE CONDITIONS ARE THAT HE WON'T SHOOT YOU"
1520 PRINT "IF YOU TAKE THE FIRST STARE OUT OF TOWN AND NEVER COME BACK"
1530 PRINT "AGREED."
1540 INPUT H$
1550 IF H$ = "NO" THEN 1590
1570 PRINT "A VERY WISE DECISION."
1580 GOTO 2080
1590 PRINT "OH WELL, BACK TO THE SHOWDOWN"
1600 GOTO 550
1610 PRINT "HOW FAR DID YOU RUN";
1620 INPUT F
1630 IF F < 50 THEN 1670
1640 PRINT "MAN, DID HE RUN. HE RAN SO FAST EVEN THE DOGS COULDN'T"
1650 PRINT "CATCH HIM"
1660 GOTO 2080
1670 IF P >= 4 THEN 1770
1680 PRINT "BLACK BART FIRES";4 - P;"SHELLS......."
1690 IF P >= 4 THEN 1730
1700 PRINT "HE GOT YOU RIGHT IN THE BACK. THATS WHAT YOU DESERVE"
1710 PRINT "FOR RUNNING."
1720 GOTO 2080
1730 PRINT "BLACK BART UNLOADED HIS GUN,ONCE IN YOUR BACK"
1740 PRINT "AND"3 - P"TIMES IN YOUR A**, NOW YOU CAN'T EVEN REST IN"
1750 PRINT "PEACE."
1760 GOTO 2080
1770 PRINT "YOU WERE LUCKY. BART CAN ONLY THROW HIS GUN AT YOU, HE"
1780 PRINT "DOESN'T HAVE ANY SHELLS LEFT. YOU SHOULD REALLY BE DEAD."
1790 GOTO 2080
1800 IF P = 2 THEN 1940
1810 IF P = 3 THEN 1960
1820 PRINT "GRAZED BART IN THE RIGHT ARM."
1830 GOTO 1040
1840 PRINT "HE'S HIT IN THE LEFT SHOULDER,FORCING HIM TO USE HIS RIGHT"
1850 PRINT "HAND TO SHOOT WITH"
1860 GOTO 1040
1870 PRINT "THAT WAS YOUR LAST SHOT.YOU MISSED"
1880 GOTO 1040
1890 PRINT "BUT BART GOT YOU IN THE RIGHT SHIN."
1900 GOTO 550
1910 PRINT "THAT TRICK JUST SAVED YOUR LIFE. BART'S BULLET"
1920 PRINT "WAS STOPPED BY THE WOOD SIDES OF THE TROUGH."
1930 GOTO 550
1940 PRINT "THOUGH BART GOT YOU ON THE LEFT SIDE OF YOUR JAW."
1950 GOTO 550
1960 PRINT "BART MUST HAVE JERKED THE TRIGGER"
1970 GOTO 550
1980 PRINT "NOBODY CAN WALK THAT FAST"
1990 GOTO 620
2000 PRINT "NONE OF THIS NEGATIVE STUFF PARTNER. ONLY POSITIVE NUMBERS."
2010 GOTO 620
2020 PRINT "BART JUST HI-TAILED IT OUT OF TOWN RATHER THAN FACE YOU WITH-"
2030 PRINT "OUT A LOADED GUN. YOU CAN REST ASSURED THAT BART WON'T EVER"
2040 PRINT "SHOW HIS FACE AROUND THIS TOWN AGAIN."
2050 GOTO 2080
2060 PRINT "HOW MANY WATERING TROUGHS DO YOU THINK ARE ON THIS STREET"
2070 GOTO 550
2080 PRINT
2090 PRINT "C.G. INC."
2100 REM THE VARIABLE USAGE IS AS FOLLOWS:
2110 REM D$--INSTRUCTION NEED
2120 REM A$--INTENT TO CONTIUE THE GAME AFTER RECEIVING INSTRUCTIONS
2130 REM A---STORES YOUR LUCKY NUMBER
2140 REM I---USED FOR THE RANDOM GENERATOR
2150 REM X---DENOTES THE DISTANCE BETWEEN YOU
2160 REM B---DENOTES THE NUMBER OF YOUR MOVE
2170 REM S---THE NUMBER OF PACES YOU ADVANCE
2180 REM C---COUNTER FOR THE NUMBER OF YOU SHELLS
2190 REM W---RANDOM NUMBER FOR THE HITTING OF BART
2200 REM O---RANDOM NUMBER FOR BART'S CHOICE OF WALK OR FIRE
2210 REM Z---RANDOM NUMB BOR THE PACES BART ADVANCES
2220 REM P---COUNTER FOR BART'S REMAINING SHELLS
2230 REM R---RANDOM NUMBER FORDART HITTING YOU
2240 REM T---COUNTER FOR THE NUMBER OF WATERING TROUGHS
2250 REM F---DISTANCE YOU RAN
2260 REM H$--DECISION ON GIVING UP
2270 END
